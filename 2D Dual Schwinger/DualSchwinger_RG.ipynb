{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "40f13119",
   "metadata": {},
   "source": [
    "We now implement our standard RG procedure for scalar fields. First generate samples, then use a suitable analog of block-spin and re-learn after several steps."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a941ff72",
   "metadata": {},
   "outputs": [],
   "source": [
    "using AdvancedHMC, ForwardDiff\n",
    "using LogDensityProblems, LinearAlgebra, Random, Statistics, JuMP, Ipopt, Plots\n",
    "\n",
    "# Define the DualSchwingerModel2D structure for the 2D model\n",
    "struct DualSchwingerModel2D\n",
    "    lattice_size::Int  # Lattice size (NxN)\n",
    "    λ::Float64         # Cosine interaction strength\n",
    "    mu2::Float64       # Mass parameter squared (mu^2)\n",
    "    θ::Float64         # Constant phase shift\n",
    "end\n",
    "#A function to help normalize the values of phi\n",
    "\n",
    "\n",
    "# Function to calculate the total energy (kinetic + potential) of a 2D field configuration\n",
    "function calculate_energy_2d(phi::Matrix{T}, model::DualSchwingerModel2D) where T<:Real\n",
    "    nx, nt = size(phi)  # Dimensions of the lattice\n",
    "\n",
    "    # Initialize energy terms using the type of `phi`\n",
    "    kinetic_energy = zero(T)\n",
    "    gradient_energy = zero(T)\n",
    "    mass_energy = zero(T)\n",
    "    cosine_energy = zero(T)\n",
    "\n",
    "    # Loop through all lattice sites and calculate each term in the Hamiltonian\n",
    "    for x in 1:nx\n",
    "        for t in 1:nt\n",
    "            # Calculate the temporal derivative term using discrete differences\n",
    "            t_prev = mod1(t - 1, nt)  # Time neighbor with periodic boundary conditions\n",
    "            kinetic_energy += T(0.5) * (phi[x, t] - phi[x, t_prev])^2\n",
    "\n",
    "            # Calculate the spatial gradient term using discrete differences with `x_next` (right neighbor)\n",
    "            x_prev = mod1(x - 1, nx)  # Spatial right neighbor with periodic boundary conditions\n",
    "            gradient_energy += T(0.5) * (phi[x, t] - phi[x_prev, t])^2\n",
    "\n",
    "            # Mass term using mu^2\n",
    "            mass_energy += T(0.5) * model.mu2 * phi[x, t]^2\n",
    "\n",
    "            # Cosine interaction term\n",
    "            cosine_energy += -model.λ * cos((Float64(sqrt(4*π)) * phi[x, t]) - model.θ)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Return the total energy, scaled by χ\n",
    "    total_energy = (kinetic_energy + gradient_energy + mass_energy + cosine_energy)\n",
    "    return total_energy\n",
    "end\n",
    "\n",
    "# Define the DualSchwingerModelDensity2D structure for 2D\n",
    "struct DualSchwingerModelDensity2D\n",
    "    dim::Int\n",
    "    model::DualSchwingerModel2D\n",
    "end\n",
    "\n",
    "# Implement the log-density function for DualSchwingerModelDensity2D\n",
    "function LogDensityProblems.logdensity(p::DualSchwingerModelDensity2D, phi_vector::Vector{T}) where T<:Real\n",
    "    # Reshape the vector back into a 2D matrix\n",
    "    N = p.model.lattice_size\n",
    "    phi = reshape(phi_vector, N, N)\n",
    "    return -calculate_energy_2d(phi, p.model)\n",
    "end\n",
    "\n",
    "LogDensityProblems.dimension(p::DualSchwingerModelDensity2D) = p.model.lattice_size^2\n",
    "LogDensityProblems.capabilities(::Type{DualSchwingerModelDensity2D}) = LogDensityProblems.LogDensityOrder{2}()\n",
    "\n",
    "# Function to generate data for the model using Hamiltonian Monte Carlo (HMC)\n",
    "function generate_DSM_data_hmc_2d(model::DualSchwingerModel2D, n_samples::Int, n_adapts::Int = 3000)\n",
    "    lattice_size = model.lattice_size\n",
    "    phi4_target = DualSchwingerModelDensity2D(lattice_size^2, model)\n",
    "\n",
    "    # Define the initial 2D field configuration (NxN matrix)\n",
    "    initial_phi = randn(lattice_size, lattice_size)\n",
    "\n",
    "    # Define a Hamiltonian system\n",
    "    metric = DiagEuclideanMetric(lattice_size^2)\n",
    "    hamiltonian = Hamiltonian(metric, phi4_target, ForwardDiff)\n",
    "\n",
    "    # Define a leapfrog solver with an initial step size chosen heuristically\n",
    "    initial_epsilon = find_good_stepsize(hamiltonian, vec(initial_phi))\n",
    "    integrator = Leapfrog(initial_epsilon)\n",
    "\n",
    "    # Define an HMC sampler with Multinomial sampling and Generalized No-U-Turn\n",
    "    kernel = HMCKernel(Trajectory{MultinomialTS}(integrator, GeneralisedNoUTurn()))\n",
    "    adaptor = StanHMCAdaptor(MassMatrixAdaptor(metric), StepSizeAdaptor(0.8, integrator))\n",
    "\n",
    "    # Run the sampler to draw samples from the model\n",
    "    samples_vector, _ = sample(hamiltonian, kernel, vec(initial_phi), n_samples, adaptor, n_adapts; progress=false)\n",
    "\n",
    "    # Reshape the samples back into 2D matrices\n",
    "    samples = [reshape(sample, lattice_size, lattice_size) for sample in samples_vector]\n",
    "\n",
    "    return samples\n",
    "end\n",
    "\n",
    "# Score matching objective function for 2D using mu^2\n",
    "# Score matching objective function for 2D using mu^2\n",
    "function score_matching_objective_2d(λ::T, mu2::T, θ::T, data::Vector{Matrix{Float64}}) where T<:Real\n",
    "    nx, nt = size(data[1])\n",
    "    n_samples = length(data)\n",
    "    obj = zero(T)\n",
    "    β =  Float64(sqrt(4*π))\n",
    "    \n",
    "    for sample in data\n",
    "        for x in 1:nx\n",
    "            for t in 1:nt\n",
    "                phi_xt = sample[x, t]\n",
    "\n",
    "                # Nearest neighbors with periodic boundary conditions\n",
    "                t_prev = mod1(t - 1, nt)\n",
    "                x_prev = mod1(x - 1, nx)\n",
    "                t_next = mod1(t + 1, nt)\n",
    "                x_next = mod1(x + 1, nx)\n",
    "                \n",
    "                # Sum of neighboring phi values\n",
    "                sum_neighbor_phi = sample[x, t_prev] + sample[x, t_next] + sample[x_prev, t] + sample[x_next, t]\n",
    "\n",
    "                # Correct score function s(phi_xt)\n",
    "                score_xt = -((4 * phi_xt - sum_neighbor_phi) + mu2 * phi_xt + λ * β * sin(β * phi_xt - θ))\n",
    "\n",
    "                # Correct second derivative s'(phi_xt)\n",
    "                s_prime = -(4 + mu2 + λ * β^2 * cos(β * phi_xt - θ))\n",
    "\n",
    "                # Add to the objective\n",
    "                obj += T(0.5) * score_xt^2 + s_prime\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Normalize the objective by the number of samples\n",
    "    return (obj / n_samples)\n",
    "end\n",
    "\n",
    "const RealMatrix = Matrix{Float64}\n",
    "#Block-spin RG function for 2D lattice\n",
    "function block_spin_rg(samples::Vector{RealMatrix})\n",
    "    new_samples = Vector{RealMatrix}(undef, length(samples))\n",
    "    for (i, φ) in enumerate(samples)\n",
    "        N = size(φ,1)\n",
    "        N2 = div(N,2)\n",
    "        φ_new = zeros(Float64, N2, N2)\n",
    "        for ix in 1:N2, it in 1:N2\n",
    "            # average over 2x2 block\n",
    "            block = φ[2ix-1:2ix, 2it-1:2it]\n",
    "            φ_new[ix,it] = median(vec(block))\n",
    "        end\n",
    "        new_samples[i] = φ_new\n",
    "    end\n",
    "    return new_samples\n",
    "end\n",
    "\n",
    "# Function to estimate the parameters using score matching in 2D with mu^2\n",
    "function estimate_parameters_score_matching_2d(data::Vector{Matrix{Float64}})    \n",
    "    model = Model(Ipopt.Optimizer)\n",
    "    set_optimizer_attribute(model, \"print_level\", 0)\n",
    "\n",
    "    # Define optimization variables with mu2 instead of μ\n",
    "    @variable(model, λ)\n",
    "    @variable(model, mu2)\n",
    "    @variable(model, 0.0 <= θ <= Float64(2*π))\n",
    "    \n",
    "    # Register the objective function using a wrapper that splats the inputs\n",
    "    score_matching_wrapper = (λ, mu2, θ) -> score_matching_objective_2d(λ, mu2, θ, data)\n",
    "    register(model, :score_matching_obj, 3, score_matching_wrapper; autodiff=true)\n",
    "\n",
    "    # Set the nonlinear objective\n",
    "    @NLobjective(model, Min, score_matching_obj(λ, mu2, θ))\n",
    "    \n",
    "    # Run the optimization\n",
    "    optimize!(model)\n",
    "    println(\"Final objective value: \", objective_value(model))\n",
    "    println(\"Optimizer termination status: \", termination_status(model))\n",
    "\n",
    "    if !(termination_status(model) in [MOI.OPTIMAL, MOI.LOCALLY_SOLVED])\n",
    "        @warn \"Optimization did not converge to an optimal solution\"\n",
    "    end\n",
    "    \n",
    "    # Retrieve optimized parameter values\n",
    "    estimated_lambda = value(λ)\n",
    "    estimated_mu2 = value(mu2)\n",
    "    estimated_theta = value(θ)\n",
    "    \n",
    "    return estimated_lambda, estimated_mu2, estimated_theta\n",
    "end\n",
    "\n",
    "lattice_size = 32\n",
    "n_samples = 15000\n",
    "true_model = DualSchwingerModel2D(lattice_size, 0.8, 0.4, pi)  # Use mu^2 for true_model\n",
    "\n",
    "samples = generate_DSM_data_hmc_2d(true_model, n_samples)  # HMC sampling\n",
    "        \n",
    "# Estimate parameters using score matching\n",
    "λ_est, mu2_est, theta_est = estimate_parameters_score_matching_2d(samples)\n",
    "println(\"Estimated parameters: λ=$λ_est, mu^2=$mu2_est, θ=$theta_est\")\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.4",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
