{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "e061f865",
   "metadata": {},
   "source": [
    "We want $(\\lambda,m^2)$ phase diagram. First we generate samples, then learn them per RG step and infer RG trajectories!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a263186c",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ColorSchemes\n",
    "\n",
    "using AdvancedHMC, ForwardDiff\n",
    "using LogDensityProblems, LinearAlgebra, Random, Statistics, Optim, GalacticOptim, Plots\n",
    "\n",
    "# --------------------------\n",
    "# Model Definitions\n",
    "# --------------------------\n",
    "\n",
    "struct Phi4Model2D\n",
    "    lattice_size::Int\n",
    "    lambda::Float64\n",
    "    m2::Float64\n",
    "    a::Float64\n",
    "end\n",
    "\n",
    "function calculate_energy_2d(phi::Matrix{T}, model::Phi4Model2D) where T<:Real\n",
    "    N = size(phi, 1)\n",
    "    kinetic_energy = 0.0\n",
    "    potential_energy = 0.0\n",
    "    a = model.a\n",
    "    for i in 1:N, j in 1:N\n",
    "        i_right = mod1(i + 1, N)\n",
    "        j_up = mod1(j + 1, N)\n",
    "        kinetic_energy += 0.5 * ((phi[i, j] - phi[i_right, j])^2 + (phi[i, j] - phi[i, j_up])^2)/a^2\n",
    "        potential_energy += 0.5 * model.m2 * phi[i, j]^2 + 0.25 * model.lambda * phi[i, j]^4 \n",
    "    end\n",
    "    return kinetic_energy + potential_energy\n",
    "end\n",
    "\n",
    "struct Phi4TargetDensity2D\n",
    "    dim::Int\n",
    "    model::Phi4Model2D\n",
    "end\n",
    "\n",
    "function LogDensityProblems.logdensity(p::Phi4TargetDensity2D, phi_vector::Vector{T}) where T<:Real\n",
    "    N = p.model.lattice_size\n",
    "    phi = reshape(phi_vector, N, N)\n",
    "    return -calculate_energy_2d(phi, p.model)\n",
    "end\n",
    "\n",
    "LogDensityProblems.dimension(p::Phi4TargetDensity2D) = p.model.lattice_size^2\n",
    "LogDensityProblems.capabilities(::Type{Phi4TargetDensity2D}) = LogDensityProblems.LogDensityOrder{1}()\n",
    "\n",
    "function generate_phi4_data_hmc_2d(model::Phi4Model2D, n_samples::Int, n_adapts::Int = 2000)\n",
    "    lattice_size = model.lattice_size\n",
    "    phi4_target = Phi4TargetDensity2D(lattice_size^2, model)\n",
    "    initial_phi = randn(lattice_size, lattice_size)\n",
    "    metric = DiagEuclideanMetric(lattice_size^2)\n",
    "    hamiltonian = Hamiltonian(metric, phi4_target, ForwardDiff)\n",
    "    initial_epsilon = find_good_stepsize(hamiltonian, vec(initial_phi))\n",
    "    integrator = Leapfrog(initial_epsilon)\n",
    "    kernel = HMCKernel(Trajectory{MultinomialTS}(integrator, GeneralisedNoUTurn()))\n",
    "    adaptor = StanHMCAdaptor(MassMatrixAdaptor(metric), StepSizeAdaptor(0.8, integrator))\n",
    "    samples_vector, stats = sample(hamiltonian, kernel, vec(initial_phi), n_samples, adaptor, n_adapts; progress=false)\n",
    "    samples = [reshape(sample, lattice_size, lattice_size) for sample in samples_vector]\n",
    "    return samples\n",
    "end\n",
    "\n",
    "function score_matching_objective_2d(lambda::Float64, m2::Float64, a::Float64, data::Vector{Matrix{Float64}})\n",
    "    N = size(data[1], 1)\n",
    "    obj = 0.0\n",
    "    for sample in data\n",
    "        for i in 1:N, j in 1:N\n",
    "            x_ij = sample[i, j]\n",
    "            x_left = sample[mod1(i - 1, N), j]\n",
    "            x_right = sample[mod1(i + 1, N), j]\n",
    "            x_down = sample[i, mod1(j - 1, N)]\n",
    "            x_up = sample[i, mod1(j + 1, N)]\n",
    "            kinetic_term = -(4 * x_ij - x_left - x_right - x_up - x_down)/a^2\n",
    "            score_ij = kinetic_term - (m2 * x_ij + lambda * x_ij^3)\n",
    "            s_prime = -(m2 + 3 * lambda * x_ij^2 + 4/a^2)\n",
    "            obj += 0.5 * score_ij^2 + s_prime\n",
    "        end\n",
    "    end\n",
    "    return obj / length(data)\n",
    "end\n",
    "\n",
    "function estimate_parameters_score_matching_2d(data::Vector{Matrix{Float64}})\n",
    "    f(x) = score_matching_objective_2d(x[1], x[2], x[3], data)\n",
    "    initial_x = [0.1, 0.1, 0.8]\n",
    "    result = optimize(f, initial_x, ParticleSwarm())\n",
    "    x_opt = result.minimizer\n",
    "    println(\"Estimated λ = \", x_opt[1], \", m² = \", x_opt[2], \", a = \", x_opt[3])\n",
    "    return x_opt[1], x_opt[2], x_opt[3]\n",
    "end\n",
    "\n",
    "function compute_raw_grad_squared(phi::Matrix{Float64})\n",
    "    gradx = circshift(phi, (-1, 0)) .- phi\n",
    "    grady = circshift(phi, (0, -1)) .- phi\n",
    "    return mean(gradx.^2 .+ grady.^2)\n",
    "end\n",
    "\n",
    "function block_spin_transform_with_normalization(phi::Matrix{Float64})\n",
    "    nx, ny = size(phi)\n",
    "    nx_new, ny_new = div(nx, 2), div(ny, 2)\n",
    "    phi_blocked = zeros(Float64, nx_new, ny_new)\n",
    "    for i in 1:nx_new, j in 1:ny_new\n",
    "        phi_blocked[i, j] = median(vec(phi[2i-1:2i, 2j-1:2j]))\n",
    "    end\n",
    "    raw_grad_old = compute_raw_grad_squared(phi)\n",
    "    raw_grad_new = compute_raw_grad_squared(phi_blocked)\n",
    "    Z_phi = raw_grad_new / raw_grad_old\n",
    "    phi_blocked_normalized = phi_blocked ./ sqrt(Z_phi)\n",
    "    return phi_blocked_normalized\n",
    "end\n",
    "\n",
    "function block_spin_samples(samples::Vector{Matrix{Float64}})\n",
    "    return [block_spin_transform_with_normalization(sample) for sample in samples]\n",
    "end\n",
    "\n",
    "# Run RG flow and collect (λ, m²) trajectory\n",
    "function run_lambda_vs_m2_trajectory(lambda_0::Float64, m2_0::Float64, lattice_size::Int, n_samples::Int, n_steps::Int)\n",
    "    model = Phi4Model2D(lattice_size, lambda_0, m2_0, 1.0)\n",
    "    samples = generate_phi4_data_hmc_2d(model, n_samples)\n",
    "    lambdas = [lambda_0]\n",
    "    m2s = [m2_0]\n",
    "    for step in 1:(n_steps - 1)\n",
    "        samples = block_spin_samples(samples)\n",
    "        λ, m2, _ = estimate_parameters_score_matching_2d(samples)\n",
    "        push!(lambdas, λ)\n",
    "        push!(m2s, m2)\n",
    "    end\n",
    "    return lambdas, m2s\n",
    "end\n",
    "\n",
    "function scan_and_plot_lambda_vs_m2(lambda_fixed::Float64, m2_range::Vector{Float64}, lattice_size::Int, n_samples::Int, n_steps::Int)\n",
    "    plot(xlabel=\"m²\", ylabel=\"λ\", title=\"RG trajectories in (m², λ) space\", legend=:topleft)\n",
    "    for m2_0 in m2_range\n",
    "        lambdas, m2s = run_lambda_vs_m2_trajectory(lambda_fixed, m2_0, lattice_size, n_samples, n_steps)\n",
    "        color_index = findfirst(==(m2_0), m2_range)\n",
    "        plot_color = get(ColorSchemes.RdYlBu_8, (color_index - 1) / (length(m2_range) - 1))\n",
    "        plot!(m2s, lambdas, label=\"m²₀ = $m2_0\", lw=2, marker=:circle, color=plot_color)\n",
    "\n",
    "        for i in 1:length(m2s)-1\n",
    "            dx = m2s[i+1] - m2s[i]\n",
    "            dy = lambdas[i+1] - lambdas[i]\n",
    "            quiver!([m2s[i]], [lambdas[i]], quiver=([dx], [dy]), arrow=:closed, color=plot_color, lw=1, linealpha=1.0, marker=false)\n",
    "        end\n",
    "    end\n",
    "    savefig(\"rg_lambda_vs_m2.pdf\")\n",
    "    display(current())\n",
    "end\n",
    "\n",
    "# Run scan and plot\n",
    "lattice_size = 16\n",
    "lambda_0 = 0.5\n",
    "m2_range = collect(-1.5:0.25:0.25)\n",
    "n_samples = 20000\n",
    "n_steps = 3\n",
    "scan_and_plot_lambda_vs_m2(lambda_0, m2_range, lattice_size, n_samples, n_steps)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5e19f3f1",
   "metadata": {},
   "source": [
    "We can also scan the diagram for various values of $\\lambda$ and $m^2$ to see where the critical point is for each."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bcfc0b57",
   "metadata": {},
   "outputs": [],
   "source": [
    "function scan_phase_diagram(lambda_range::Vector{Float64}, m2_range::Vector{Float64},\n",
    "    lattice_size::Int, n_samples::Int, n_steps::Int)\n",
    "\n",
    "    results = []\n",
    "\n",
    "    for λ in lambda_range\n",
    "        println(\"Scanning λ = $λ\")\n",
    "        transition_found = false\n",
    "\n",
    "        for i in 1:length(m2_range)-1\n",
    "            m2a = m2_range[i]\n",
    "            m2b = m2_range[i+1]\n",
    "\n",
    "            _, m2s_a = run_lambda_vs_m2_trajectory(λ, m2a, lattice_size, n_samples, n_steps)\n",
    "            _, m2s_b = run_lambda_vs_m2_trajectory(λ, m2b, lattice_size, n_samples, n_steps)\n",
    "\n",
    "            final_m2a = m2s_a[end]\n",
    "            final_m2b = m2s_b[end]\n",
    "\n",
    "            if sign(final_m2a - m2a) != sign(final_m2b - m2b)\n",
    "                m2c_estimate = (m2a + m2b) / 2\n",
    "                println(\"  Critical m²_c ≈ $m2c_estimate\")\n",
    "                push!(results, (λ = λ, mc2 = m2c_estimate))\n",
    "                transition_found = true\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "\n",
    "        if !transition_found\n",
    "            println(\"  No transition detected in given m² range for λ = $λ\")\n",
    "            push!(results, (λ = λ, mc2 = NaN))\n",
    "        end\n",
    "    end\n",
    "    return results\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "345450fa",
   "metadata": {},
   "outputs": [],
   "source": [
    "lambda_range = collect(0.1:0.1:0.6)\n",
    "m2_range = collect(-0.6:0.05:-0.0)\n",
    "results = scan_phase_diagram(lambda_range, m2_range, 16, 15000, 3)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "286ec663",
   "metadata": {},
   "source": [
    "Code to plot the critical points in the $\\lambda$ and $m^2$ phase space. We also store the samples from a previous run. Comment out the values if you need to run this separately."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "38991b8a",
   "metadata": {},
   "outputs": [],
   "source": [
    "using LaTeXStrings, Plots, ColorSchemes\n",
    "# Stored values from previous runs\n",
    "mc2s = [-0.58, -0.52, -0.46, -0.40, -0.35, -0.30, -0.27, -0.25, -0.23]\n",
    "λs    = [ 0.55,  0.50,  0.45,  0.40,  0.35,  0.30,  0.25,  0.20,  0.15]\n",
    "# Phase boundary plot\n",
    "\n",
    "p = plot(\n",
    "    mc2s[1:6], λs[1:6],\n",
    "    marker = :circle,\n",
    "    lw = 2,\n",
    "    xlabel = L\"m^2_c\",\n",
    "    ylabel = L\"\\lambda\",\n",
    "    title = L\"Phase Boundary $m^2_c(\\lambda)$\",\n",
    "    palette = :RdBu_4,\n",
    "    label = \"Phase boundary\"\n",
    ")\n",
    "\n",
    "# Symmetric phase (fill below)\n",
    "plot!(\n",
    "    mc2s[1:6], λs[1:6],\n",
    "    fillrange = minimum(λs) .- 0.05,\n",
    "    fillalpha = 0.3,\n",
    "    label = \"Broken symmetry phase\",\n",
    "    color = get(ColorSchemes.RdYlBu, 0.85),  # bluish\n",
    "    lw = 0\n",
    ")\n",
    "\n",
    "# Z2 broken phase (fill above)\n",
    "plot!(\n",
    "    mc2s[1:6], λs[1:6],\n",
    "    fillrange = maximum(λs) .+ 0.05,\n",
    "    fillalpha = 0.3,\n",
    "    label = \"Symmetric phase\",\n",
    "    color = get(ColorSchemes.RdYlBu, 0.15),  # reddish\n",
    "    lw = 0\n",
    ")\n",
    "\n",
    "savefig(p, \"phase_bdy_lambdam^2.pdf\")\n",
    "display(p)\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.4",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
